apply plugin: 'com.android.application'

android {
  compileSdkVersion 22
  buildToolsVersion "22.0.1"

  signingConfigs {
    release {
    }
  }

  defaultConfig {
    applicationId "net.thoj.celebratepride"
    minSdkVersion 14
    targetSdkVersion 22
    versionCode 5
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles 'proguard-android.txt'
      signingConfig signingConfigs.release
    }
    debug {
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:22.1.1'
  compile 'com.android.support:cardview-v7:22.1.1'

  compile 'com.jakewharton:butterknife:6.1.0'
  compile 'com.squareup.picasso:picasso:2.5.2'
}

File propFile = file('signing.properties');
if (propFile.exists()) {
  def Properties props = new Properties()
  props.load(new FileInputStream(propFile))

  if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
      props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
  } else {
    android.buildTypes.release.signingConfig = null
  }
} else {
  android.buildTypes.release.signingConfig = null
}
